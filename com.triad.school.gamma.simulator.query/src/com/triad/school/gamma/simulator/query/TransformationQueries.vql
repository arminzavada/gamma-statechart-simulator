package com.triad.school.gamma.simulator.query

import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart"
import "http://www.mit.bme.hu/gamma/expression/Model"
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface"
import "http://www.triad.com/school/gamma/simulator/model"

pattern rootRegion(region: Region) {
	StatechartDefinition.regions(_, region);	
}

pattern subRegion(parentRegion: Region, region: Region) {
	Region.stateNodes(parentRegion, node);
	CompositeElement.regions(node, region);
}

pattern associatedRegionalActiveState(regionalActiveState: RegionalActiveState, state: StateNode) {
	RegionalActiveState.region(regionalActiveState, region);
	Region.stateNodes(region, state);
}

pattern activeState(state: StateNode) {
	RegionalActiveState.state(_, state);
}

pattern regionalState(regionalActiveState: RegionalActiveState, region: Region) {
	RegionalActiveState.region(regionalActiveState, region);
}

pattern statechartInputPort(port: Port) {
	StatechartDefinition.ports(_, port);
	Port.interfaceRealization(port, interfaceRealization);
	InterfaceRealization.realizationMode(interfaceRealization, ::REQUIRED);
}

pattern variables(variable: VariableDeclaration) {
	StatechartDefinition.variableDeclarations(_, variable);
}

pattern allStates(node: StateNode) {
	Region.stateNodes(_, node);
}

private pattern parentRegion(region: Region, parentRegion: Region) {
	Region.stateNodes(region, state);
	find compositeState(parentState, state);
	Region.stateNodes(parentRegion, parentState);
}

private pattern historyRegion(region: Region) {
	Region.stateNodes(region, state);
	ShallowHistoryState(state);
} or {
	find parentRegion*(region, parentRegion);
	Region.stateNodes(parentRegion, state);
	DeepHistoryState(state);	
}

pattern historyEntryState(region: Region, state: StateNode) {
	find historyRegion(region);
	RegionalActiveState.region(ras, region);
	RegionalActiveState.last(ras, state);
}

pattern entryState(region: Region, state: EntryState) {
	Region.stateNodes(region, state);
}

pattern compositeState(parent: State, child: StateNode) {
	State.regions(parent, region);
	Region.stateNodes(region, child);
}

pattern descendantState(parent: State, descendant: StateNode) {
	find compositeState+(parent, descendant);
}

pattern fireablePseudoStateTransition(region: Region, transition: Transition) {
	Transition.sourceState(transition, source);
	PseudoState(source);
	Region.stateNodes(region, source);
	
	find activeState(source);
}

pattern fireableTransition(region: Region, transition: Transition, event: Event) {
	Transition.sourceState(transition, source);
	Region.stateNodes(region, source);
	
	find activeState(source);
	
	Transition.trigger(transition, trigger);
	EventTrigger.eventReference(trigger, eventReference);
	PortEventReference.event(eventReference, event);
}