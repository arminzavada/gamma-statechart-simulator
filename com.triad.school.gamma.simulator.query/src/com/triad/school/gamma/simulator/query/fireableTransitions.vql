package com.triad.school.gamma.simulator.query

import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart"
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface"
import "http://www.triad.com/school/gamma/simulator/active_state"

pattern activeState(state: StateNode) {
	ActiveStateContainer(container);
	ActiveStateContainer.activeState(container, state);
}

pattern ports(port: Port) {
	Port.interfaceRealization(port, interfaceRealization);
	InterfaceRealization.interface(interfaceRealization, interface);
	InterfaceRealization.realizationMode(interfaceRealization, ::REQUIRED);
}

pattern initialNode(state: InitialState) {
	InitialState(state);
}

pattern fireableNonTriggerTransition(source: StateNode, transition: Transition, target: StateNode) {
	Transition.sourceState(transition, source);
	Transition.targetState(transition, target);
	
	find activeState(source);
	
	neg Transition.trigger(transition, _trigger);
}

pattern fireableTriggerTransition(source: StateNode, transition: Transition, target: StateNode, event: Event) {
	Transition.sourceState(transition, source);
	Transition.targetState(transition, target);
	
	find activeState(source);
	
	Transition.trigger(transition, trigger);
	EventTrigger.eventReference(trigger, eventReference);
	PortEventReference.event(eventReference, event);
}