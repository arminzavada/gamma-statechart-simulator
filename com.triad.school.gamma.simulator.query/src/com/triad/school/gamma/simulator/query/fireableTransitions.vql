package com.triad.school.gamma.simulator.query

import "http://www.mit.bme.hu/gamma/statechart/Model/Statechart"
import "http://www.mit.bme.hu/gamma/statechart/Model/Interface"
import "http://www.triad.com/school/gamma/simulator/model"

pattern rootRegion(region: Region) {
	StatechartDefinition.regions(_, region);	
}

pattern subRegion(parentRegion: Region, region: Region) {
	Region.stateNodes(parentRegion, node);
	CompositeElement.regions(node, region);
}

pattern activeRegion(region: Region) {
	StatechartDefinition.regions(_, region);
} or {
	find activeStateNode(_, _, state);
	CompositeElement.regions(state, region);
}

pattern activeStateNode(activeState: ActiveState, region: Region, state: StateNode) {
	ActiveState.containingRegion(activeState, region);
	ActiveState.state(activeState, state);
}

pattern statechartInputPort(port: Port) {
	StatechartDefinition.ports(_, port);
	Port.interfaceRealization(port, interfaceRealization);
	InterfaceRealization.realizationMode(interfaceRealization, ::REQUIRED);
}

pattern allStates(node: StateNode) {
	Region.stateNodes(_, node);
}

pattern initialNode(region: Region, state: InitialState) {
	Region.stateNodes(region, state);
}

pattern fireableEmptyTransition(activeState: ActiveState, transition: Transition) {
	find activeRegion(region);
	find activeStateNode(activeState, region, source);
	Transition.sourceState(transition, source);
	
	neg Transition.trigger(transition, _);
}

pattern fireableTriggerTransition(activeState: ActiveState, transition: Transition, event: Event) {
	ActiveState.state(activeState, source);
	Transition.sourceState(transition, source);
	
	Transition.trigger(transition, trigger);
	EventTrigger.eventReference(trigger, eventReference);
	PortEventReference.event(eventReference, event);
}